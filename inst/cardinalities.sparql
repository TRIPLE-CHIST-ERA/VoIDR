# data properties:
PREFIX sh:<http://www.w3.org/ns/shacl#>
PREFIX sd:<http://www.w3.org/ns/sparql-service-description#>
PREFIX void:<http://rdfs.org/ns/void#>
PREFIX void_ext:<http://ldf.fi/void-ext#>
SELECT DISTINCT  ?property ?datatypeTo ?classFrom ?cardinalities
where {
  ?cp1 void:class ?classFrom .
  ?cp1 void:propertyPartition ?pp1 .
  ?pp1 void:property ?property .
  ?pp1 void:triples ?triples .
  {
    ?pp1 void_ext:datatypePartition ?cp2 .
    ?cp2 void_ext:datatype ?datatypeTo .
  }

{
###
SELECT
    ?property
    ( CONCAT( MAX( ?subject_cardinality ), '..', MAX( ?object_cardinality )) AS ?cardinalities ) # NB: "1" is smaller than "n"
WHERE{
    {
        SELECT DISTINCT
            ?graph
            ?property
            ?distinct_subjects
            ?triples
            ?distinct_objects
            ( IF( ?distinct_subjects = ?triples, "1", "n" ) AS ?subject_cardinality )
            ( IF( ?distinct_objects  = ?triples, "1", "n" ) AS ?object_cardinality  )
        WHERE{
            ?graph a sd:Graph ;
                void:propertyPartition ?property_partition .
            ?property_partition
                void:property         ?property          ;
                void:distinctSubjects ?distinct_subjects ;
                void:triples          ?triples           ;
                void:distinctObjects  ?distinct_objects  .
        }
    }
}
GROUP BY ?property
###
}
} ORDER BY ?classFrom ?cardinalities



#### class properties:

SELECT DISTINCT  ?property ?classTo ?classFrom ?cardinalities
where {
  ?cp1 void:class ?classFrom .
  ?cp1 void:propertyPartition ?pp1 .
  ?pp1 void:property ?property .
  ?pp1 void:triples ?triples .
{
    ?pp1 void:classPartition ?cp2 .
    ?cp2 void:class ?classTo .
    ?graph void:classPartition ?cp3 .
    ?cp3 void:class ?classTo .
  }
{
###
SELECT
    ?property
    ( CONCAT( MAX( ?subject_cardinality ), '..', MAX( ?object_cardinality )) AS ?cardinalities ) # NB: "1" is smaller than "n"
WHERE{
    {
        SELECT DISTINCT
            ?graph
            ?property
            ?distinct_subjects
            ?triples
            ?distinct_objects
            ( IF( ?distinct_subjects = ?triples, "1", "n" ) AS ?subject_cardinality )
            ( IF( ?distinct_objects  = ?triples, "1", "n" ) AS ?object_cardinality  )
        WHERE{
            ?graph a sd:Graph ;
                void:propertyPartition ?property_partition .
            ?property_partition
                void:property         ?property          ;
                void:distinctSubjects ?distinct_subjects ;
                void:triples          ?triples           ;
                void:distinctObjects  ?distinct_objects  .
        }
    }
}
GROUP BY ?property
###
}
} ORDER BY ?classFrom ?cardinalities
